-- 1
-- Modeling the Economic Indicators Data with DAX
-- Objective: Adding indicators like Population, GDP, GDP per capita and other measures for these indicators providing the latest data for each country

-- 1.a
-- Create measure named SumIndicatorValue:
SumIndicatorValue = 
SUM(
Indicators[IndicatorValue]
)

-- 1.b
-- Create measure named Population:
Population = 
CALCULATE(
    [SumIndicatorValue],
    Indicators[Series Code] = "SP.POP.TOTL"
)

-- 1.c
-- Create measure named GDP:
GDP = 
CALCULATE(
    [SumIndicatorValue], 
    Indicators[Series Code]="NY.GDP.MKTP.KD"
)

-- 1.d
-- Create measure named GDP per Capita:
GDP per capita = 
DIVIDE(
    [GDP],[Population],0
)

-- 1.e
-- Create measure named 2020 GDP:
2020 GDP = 
CALCULATE(
    [GDP], Indicators[Year] = 2020
)

-- 1.f
-- Create measure named 2020 GDP per capita:
2020 GDP per capita = 
DIVIDE(
    [2020 GDP],[2020 Population],0
)

-- 1.g
-- Create measure named 2020 Population:
2020 Population = 
CALCULATE(
    [Population], Indicators[Year]=2020
)

-- 1.h
-- Create measure named 2020 Pop.Density (Population Density):
2020 Pop.Density = 
DIVIDE(
    [2020 Population],SUM(Countries[Land Area]),0
)

--2. Using Variables for Complex DAX
-- Objective:calculate GDP Annual Growth % by year

-- Create a measure named GDPAnnualGrowth_%
GDPAnnualGrowth_% = 
var CurrentYear = SELECTEDVALUE(Indicators[Year])
var CurrentYearGDP = CALCULATE([GDP],Indicators[Year]=CurrentYear)
var PreYearGDP = CALCULATE([GDP],Indicators[Year]=CurrentYear-1)
var Diff = CurrentYearGDP - PreYearGDP
var Growthrate = DIVIDE(Diff,PreYearGDP,0)
return
Growthrate
